chapter 2b, basic containers
============================

Section 2bA, units
------------------

### `++biff`

Unit as argument

Applies a function `b` that produces a unit to the unwrapped value of [++unit]()
`a` (`u.a`). If `a` is empty, `~` is produced.

Accepts
-------

`a` is a unit.

`b` is a function that accepts a noun and produces a unit.

Produces
--------

A unit.

Source
------

    ++  biff                                                ::  apply
      |*  [a=(unit) b=$+(* (unit))]
      ?~  a  ~
      (b u.a)

Examples
--------

    ~zod/try=> (biff (some 5) |=(a=@ (some (add a 2))))
    [~ u=7]
    ~zod/try=> (biff ~ |=(a=@ (some (add a 2))))
    ~

------------------------------------------------------------------------

### `++bind`

Non-unit function to unit, producing unit

Applies a function `b` to the value (`u.a`) of a [++unit]() `a`, producing
a unit. Used when you want a function that does not accept or produce a
unit to both accept and produce a unit.

Accepts
-------

`a` is a unit.

`b` is a function.

Produces
--------

A unit.

Source
------

    ++  bind                                                ::  argue
      |*  [a=(unit) b=gate]
      ?~  a  ~
      [~ u=(b u.a)]

Examples
--------

    ~zod/try=> (bind ((unit ,@) [~ 97]) ,@t)
    [~ `a`]
    ~zod/try=> =a |=(a=@ (add a 1))
    ~zod/try=> (bind ((unit ,@) [~ 2]) a)
    [~ 3]

------------------------------------------------------------------------

### `++bond`

Replace null

Replaces an empty [++unit]() `b` with the product of a called [trap]()
`a`. If the unit is not empty, then the original unit is produced.

Accepts
-------

`a` is a trap.

`b` is a unit.

Produces
--------

Either the product of `a` or the value inside of unit `b`.

Source
------

    ++  bond                                                ::  replace
      |*  a=trap
      |*  b=(unit)
      ?~  b  $:a
      u.b

Examples
--------

    ~zod/try=> (bex 10)
    1.024
    ~zod/try=> ((bond |.((bex 10))) ~)
    1.024
    ~zod/try=> ((bond |.((bex 10))) (slaw %ud '123'))
    123

------------------------------------------------------------------------

### `++both`

Group unit values into pair

Produces `~` if either `a` or `b` are empty. Otherwise, produces a
[++unit]() whose value is a cell of the values of two input units `a` and
`b`.

Accepts
-------

`a` is a unit.

`b` is a unit.

Produces
--------

A unit of the two initial values.

Source
------

    ++  both                                                ::  all the above
      |*  [a=(unit) b=(unit)]
      ?~  a  ~
      ?~  b  ~
      [~ u=[u.a u.b]]

Examples
--------

    ~zod/try=> (both (some 1) (some %b))
    [~ u=[1 %b]]
    ~zod/try=> (both ~ (some %b))
    ~

------------------------------------------------------------------------

### `++clap`

Apply function to two units

Applies a binary function `c`--which does not usually accept or produce a [`++unit`]()--
to the values of two units, `a` and `b`, producing a unit.

Accepts
-------

`a` is a unit.

`b` is a unit.

`c` is a function that performs a binary operation.

Produces
--------

A unit.

Source
------

    ++  clap                                                ::  combine
      |*  [a=(unit) b=(unit) c=_|=(^ +<-)]
      ?~  a  b
      ?~  b  a
      [~ u=(c u.a u.b)]

Examples
--------

    ~zod/try=> =u ((unit ,@t) [~ 'a'])
    ~zod/try=> =v ((unit ,@t) [~ 'b'])
    ~zod/try=> (clap u v |=([a=@t b=@t] (welp (trip a) (trip b))))
    [~ u="ab"] 
    ~zod/try=> =a ((unit ,@u) [~ 1])
    ~zod/try=> =b ((unit ,@u) [~ 2])
    ~zod/try=> =c |=([a=@ b=@] (add a b))
    ~zod/try=> (clap a b c)
    [~ 3]
      

------------------------------------------------------------------------

### `++drop`

Unit to list

Makes a [++list]() of the unwrapped value (`u.a`) of a [`++unit`]() `a`.

Accepts
-------

`a` is a unit.

Produces
--------

A list.

Source
------

    ++  drop                                                ::  enlist
      |*  a=(unit)
      ?~  a  ~
      [i=u.a t=~]

Examples
--------

    ~zod/try=> =a ((unit ,@) [~ 97])
    ~zod/try=> (drop a)
    [i=97 t=~] 
    ~zod/try=> =a ((unit ,@) [~])
    ~zod/try=> (drop a)
    ~

------------------------------------------------------------------------

### `++fall`

Give unit a default value

Produces a default value `b` for a [`++unit`]() `a` in cases where `a` is null.

Accepts
-------

`a` is a unit.

`b` is a [noun]() that's used as the default value.

Produces
--------

Either a noun `b` or the unwrapped value of unit `a`.

Source
------

    ++  fall                                                ::  default
      |*  [a=(unit) b=*]
      ?~(a b u.a)

Examples
--------

    ~zod/try=> (fall ~ `a`)
    `a`
    ~zod/try=> (fall [~ u=0] `a`)
    0

------------------------------------------------------------------------

### `++lift`

Curried bind

Accepts a [`++gate`]() `a` and produces a function that accepts [`++unit`]()
`b` to which it applies `a`. Used when you want a function that does not accept
or produce a unit to both accept and produce a unit.

Accepts
-------

`a` is a gate.

`b` is a unit.

Produces
--------

A unit.

Source
------

    ++  lift                                                ::  lift gate (fmap)
      |*  a=gate                                            ::  flipped
      |*  b=(unit)                                          ::  curried
      (bind b a)                                            ::  bind

Examples
--------

    ~zod/try=> ((lift dec) `(unit ,@)`~)
    ~
    ~zod/try=> ((lift dec) `(unit ,@)`[~ 20])
    [~ 19]

------------------------------------------------------------------------

### `++mate`

Choose

Accepts two units `a` and `b` whose values are expected to be
equivalent. If either is empty, then the value of the other is produced.
If neither are empty, it asserts that both values are the same and
produces that value. If the assertion fails, `++mate` crashes with
`'mate'` in the stack trace.

Accepts
-------

`a` is a unit.

`b` is a unit.

Produces
--------

A unit or crash.

Source
------

++  mate                                                ::  choose
  |*  [a=(unit) b=(unit)]
  ?~  b  a
  ?~  a  b
  ?.(=(u.a u.b) ~|('mate' !!) a)

Examples
--------

    ~zod/try=> =a ((unit ,@) [~ 97])
    ~zod/try=> =b ((unit ,@) [~ 97])
    ~zod/try=> (mate a b)
    [~ 97]
    ~zod/try=> =a ((unit ,@) [~ 97])
    ~zod/try=> =b ((unit ,@) [~])
    ~zod/try=> (mate a b)
    [~ 97]
    ~zod/try=> =a ((unit ,@) [~ 97])
    ~zod/try=> =b ((unit ,@) [~ 98])
    ~zod/try=> (mate a b)
    ! 'mate'
    ! exit

------------------------------------------------------------------------

### `++need`

Unwrap unit

Retrieve the value from a [`++unit`]() and crash if the unit is null.

Accepts
-------

`a` is a unit.

Produces
--------

Either the unwrapped value of `a` (`u.a`), or crash.

Source
------

    ++  need                                                ::  demand
      |*  a=(unit)
      ?~  a  !!
      u.a

Examples
--------

    ~zod/try=> =a ((unit ,[@t @t]) [~ ['a' 'b']])
    ~zod/try=> (need a)
    ['a' 'b']
    ~zod/try=> =a ((unit ,@ud) [~ 17])
    ~zod/try=> (need a)
    17
    ~zod/try=> =a ((unit ,@) [~])
    ~zod/try=> (need a)
    ! exit

------------------------------------------------------------------------

### `++some`

Wrap value in a unit

Takes any atom `a` and produces a [`++unit`]() with the value set to `a`.

Accepts
-------

`a` is a [noun]().

Produces
--------

A unit.

Source
------

    ++  some                                                ::  lift (pure)
      |*  a=*
      [~ u=a]

Examples
--------

    ~zod/try=> (some [`a` `b`])
    [~ u=[`a` `b`]]
    ~zod/try=> (some &)
    [~ u=%.y]

------------------------------------------------------------------------

section 2bB, lists
------------------

Reverse

### `++flop`

Produces the list `a` in reverse order.

Accepts
-------

`a` is a list.

Produces
--------

A list.

Source
------

    ++  flop                                                ::  reverse
      ~/  %flop
      |*  a=(list)
      =>  .(a (homo a))
      ^+  a
      =+  b=`_a`~
      |-
      ?~  a  b
      $(a t.a, b [i.a b])


Examples
--------

    ~zod/try=> =a (limo [1 2 3 ~])
    ~zod/try=> (flop a)
    ~[3 2 1]

### `++homo`

Homogenize

Produces a [`++list`]() whose type is a fork of all the contained types in the
list `a`. Used when you want to make all the types of the elements of a list the same.

Accepts
-------

`a` is a list.

Produces
--------

a list. 

Source
------

    ++  homo                                                ::  homogenize
      |*  a=(list)
      ^+  =<  $
        |%  +-  $  ?:(_? ~ [i=(snag 0 a) t=$])
        --
      a

Examples
--------

    ~zod/try=> lyst
    [i=1 t=[i=97 t=[i=2 t=[i=98 t=[i=[~ u=10] t=~]]]]]
    ~zod/try=> (homo lyst)
    ~[1 97 2 98 [~ u=10]]
    ~zod/try=> =a (limo [1 2 3 ~])
    ~zod/try=> a
    [i=1 t=[i=2 t=[i=3 t=~]]]
    ~zod/try=> (homo a)
    ~[1 2 3]

### `++limo`

List Constructor

Turns a null-terminated tuple into a [`++list`]().

Accepts
-------

`a` is a null-terminated tuple.

Produces
--------

A list

Source
------

    ++  limo                                                ::  listify
      |*  a=*
      ^+  =<  $
        |%  +-  $  ?~(a ~ ?:(_? i=-.a t=$ $(a +.a)))
        --
      a

Examples
--------

    ~zod/try=> (limo [1 2 3 ~])
    [i=1 t=[i=2 t=[i=3 t=~]]]

----------------------------------------------------------------------

### `++lent`

List length

Produces the length of any [`++list`]() `a` as an atom.

Accepts
-------

`a` is a list.

Produces
--------

an atom.

Source
------

    ++  lent                                                ::  length
      ~/  %lent
      |=  a=(list)
      ^-  @
      =+  b=0
      |-
      ?~  a  b 
      $(a t.a, b +(b))

Examples
--------

    ~zod/try=> (lent (limo [1 2 3 4 ~]))
    4
    ~zod/try=> (lent (limo [1 'a' 2 'b' (some 10) ~]))
    5

------------------------------------------------------------------------

### `++levy`

Logical "and" on list

Computes the Boolean logical "and" on the results of [gate]() `b` applied to each individual element in [`++list`]() `a`.

Accepts
-------

`a` is a list.

`b` is a gate.

Produces
--------

Source
------

    ++  levy
      ~/  %levy                                             ::  all of
      |*  [a=(list) b=_|=(p=* .?(p))]
      |-  ^-  ?
      ?~  a  &
      ?.  (b i.a)  |
      $(a t.a)

Examples
--------

    ~zod/try=> =a |=(a=@ (lte a 1))
    ~zod/try=> (levy (limo [0 1 2 1 ~]) a)
    %.n
    ~zod/try=> =a |=(a=@ (lte a 3))
    ~zod/try=> (levy (limo [0 1 2 1 ~]) a)
    %.y

------------------------------------------------------------------------

### `++lien`

Logical "or" on list

    ++  lien                                                ::  some of
      ~/  %lien
      |*  [a=(list) b=$+(* ?)]
      |-  ^-  ?
      ?~  a  |
      ?:  (b i.a)  &
      $(a t.a)

Computes the Boolean logical "or" on the results of applying [gate]() `b` to every element of [`++list`]() `a`.

`a` is a list.

`b` is a gate.

    ~zod/try=> =a |=(a=@ (gte a 1))
    ~zod/try=> (lien (limo [0 1 2 1 ~]) a)
    %.y
    ~zod/try=> =a |=(a=@ (gte a 3))
    ~zod/try=> (lien (limo [0 1 2 1 ~]) a)
    %.n

------------------------------------------------------------------------

### `++murn`

Maybe transform

Passes each member of [`++list`]() `a` to [gate]() `b`, which must produce a
[`++unit`]().  Produces a new list with all the results that do not produce
`~`.

Accepts
-------

`a` is a [list]().

`b` is a [gate]() that produces a [unit]().

Produces
--------

A unit.

Source
------

    ++  murn                                                ::  maybe transform
      |*  [a=(list) b=$+(* (unit))]
      |-
      ?~  a  ~
      =+  c=(b i.a)
      ?~  c
        $(a t.a)
      [i=u.c t=$(a t.a)]

Examples
--------

    ~zod/try=> =a |=(a=@ ?.((gte a 2) ~ (some (add a 10))))
    ~zod/try=> (murn (limo [0 1 2 3 ~]) a)
    [i=12 t=[i=13 t=~]]

-------------------------------------------------------------

### `++reap`

Replicate

Replicate: produces a [`++list`]() containing `a` copies of `b`.

Accepts
-------

`b` is a [noun]()

Produces
--------

A list. 

Source
------

    ++  reap                                                ::  replicate
      |*  [a=@ b=*]
      |-  ^-  (list ,_b)
      ?~  a  ~
      [b $(a (dec a))]

Examples
--------

    ~zod/try=> (reap 20 %a)
    ~[%a %a %a %a %a %a %a %a %a %a %a %a %a %a %a %a %a %a %a %a]
    ~zod/try=> (reap 5 ~s1)
    ~[~s1 ~s1 ~s1 ~s1 ~s1]
    ~zod/try=> `@dr`(roll (reap 5 ~s1) add)
    ~s5

------------------------------------------------------------------------

### `++reel`

Right fold

Right fold: moves right to left across a [`++list`]() `a`, recursively slamming
a binary [gate]() `b` with an element from `a` and an accumulator, producing
the final value of the accumulator.

Accepts
-------

`b` is a binary gate.

Produces
--------

The accumulator, which is a [noun]().

Source
------

    ++  reel                                                ::  right fold  
      ~/  %reel
      |*  [a=(list) b=_|=([* *] +<+)]
      |-  ^+  +<+.b
      ?~  a
        +<+.b
      (b i.a $(a t.a))

Examples
--------

    ~zod/try=> =sum =|([p=@ q=@] |.((add p q)))
    ~zod/try=> (reel (limo [1 2 3 4 5 ~]) sum)
    15
    ~zod/try=> =a =|([p=@ q=@] |.((sub p q)))
    ~zod/try=> (reel (limo [6 3 1 ~]) a)
    4
    ~zod/try=> (reel (limo [3 6 1 ~]) a)
    ! subtract-underflow
    ! exit

------------------------------------------------------------------------

### `++roll`

Left fold

Left fold: moves left to right across a list `a`, recursively slamming a
binary [gate]() `b` with an element from the [`++list`]() and an accumulator,
producing the final value of the accumulator.

Accepts
-------

`b` is a binary [gate]().

Produces
--------

The accumulator, which is a [noun]().

Source
------

    ++  roll                                                ::  left fold
      ~/  %roll
      |*  [a=(list) b=_|=([* *] +<+)]
      |-  ^+  +<+.b
      ?~  a
        +<+.b
      $(a t.a, b b(+<+ (b i.a +<+.b)))

Examples
--------

    ~zod/try=> =sum =|([p=@ q=@] |.((add p q)))
    ~zod/try=> (roll (limo [1 2 3 4 5 ~]) sum)
    q=15
    ~zod/try=> =a =|([p=@ q=@] |.((sub p q)))
    ~zod/try=> (roll (limo [6 3 1 ~]) a)
    ! subtract-underflow
    ! exit
    ~zod/try=> (roll (limo [1 3 6 ~]) a)
    q=4

------------------------------------------------------------------------

### `++skid`

Separate

Seperates a [`++list`]() `a` into two lists - Those elements of `a` who produce
true when slammed to [gate]() `b` and those who produce `%.n`.

Accepts
-------

`b` is a gate that accepts one argument and produces a loobean.

Produces
--------

A cell of two lists.

Source
------

    ++  skid                                                ::  separate
      |*  [a=(list) b=$+(* ?)]
      |-  ^+  [p=a q=a]
      ?~  a  [~ ~]
      =+  c=$(a t.a)
      ?:((b i.a) [[i.a p.c] q.c] [p.c [i.a q.c]])


Examples
--------

    ~zod/try=> =a |=(a=@ (gth a 1))
    ~zod/try=> (skid (limo [0 1 2 3 ~]) a)
    [p=[i=2 t=[i=3 t=~]] q=[i=0 t=[i=1 t=~]]]

------------------------------------------------------------------------

### `++skim`

Suffix

Cycles through the members of a list `a`, passing them to a gate `b` and
producing a list of all of the members that produce `%.y`. Inverse of
`++skip`.

Accepts
-------

`b` is a [gate]() that accepts one argument and produces a loobean.

Produces
--------

Source
------

++  skim                                                ::  only
  ~/  %skim
  |*  [a=(list) b=_|=(p=* .?(p))]
  |-
  ^+  a`
  ?~  a  ~
  ?:((b i.a) [i.a $(a t.a)] $(a t.a))

Examples
--------

    ~zod/try=> =a |=(a=@ (gth a 1))
    ~zod/try=> (skim (limo [0 1 2 3 ~]) a)
    [i=2 t=[i=3 t=~]]

------------------------------------------------------------------------

### `++skip`

Except

Cycles through the members of [`++list`]() `a`, passing them to a [gate]() `b`. 
Produces a list of all of the members that produce `%.n`. Inverse of
`++skim`.

Accepts
-------

`b` is a [gate]() that accepts one argument and produces a loobean.

Produces
--------

A list of the same type as `a`.

Source
------

    ++  skip                                                ::  except
      ~/  %skip
      |*  [a=(list) b=_|=(p=* .?(p))]
      |-
      ^+  a
      ?~  a  ~
      ?:((b i.a) $(a t.a) [i.a $(a t.a)])

Examples
--------

    ~zod/try=> =a |=(a=@ (gth a 1))
    ~zod/try=> (skip (limo [0 1 2 3 ~]) a)
    [i=0 t=[i=1 t=~]]

------------------------------------------------------------------------

### `++scag`

Prefix

Accepts an atom `a` and [list]() `b`, producing the first `a` elements of
the front of the list.

Accepts
-------

`b` is a list.

Produces
--------

A list of the same type as `b`.

Source
------

    ++  scag                                                ::  prefix
      ~/  %scag
      |*  [a=@ b=(list)]
      |-  ^+  b
      ?:  |(?=(~ b) =(0 a))  ~
      [i.b $(b t.b, a (dec a))]

Examples
--------

    ~zod/try=> (scag 2 (limo [0 1 2 3 ~]))
    [i=0 t=[i=1 t=~]]
    ~zod/try=> (scag 10 (limo [1 2 3 4 ~]))
    [i=1 t=[i=2 t=[i=3 t=[i=4 t=~]]]]

------------------------------------------------------------------------

### `++slag`

Suffix

Accepts an atom `a` and list `b`, producing the remaining elements from
`b` starting at `a`.

Accepts
-------

`b` is a [list]().

Produces
--------

A list of the same type as `b`.

Source
------

    ++  slag                                                ::  suffix
      ~/  %slag
      |*  [a=@ b=(list)]
      |-  ^+  b
      ?:  =(0 a)  b
      ?~  b  ~
      $(b t.b, a (dec a))

Examples
--------

    ~zod/try=> (slag 2 (limo [1 2 3 4 ~]))
    [i=3 t=[i=4 t=~]]
    ~zod/try=> (slag 1 (limo [1 2 3 4 ~]))
    [i=2 t=[i=3 t=[i=4 t=~]]]

------------------------------------------------------------------------

### `++snag`

Index

Accepts an atom `a` and a [`++list`]() `b`, producing the element at the index
of `a`and failing if the list is null. Lists are 0-indexed.

Accepts
-------

`b` is a [list]().

Produces
--------

Produces an element of `b`, or crashes if no element exists at that index.

Source
------

    ++  snag                                                ::  index
      ~/  %snag
      |*  [a=@ b=(list)]
      |-
      ?~  b
        ~|('snag-fail' !!)
      ?:  =(0 a)  i.b
      $(b t.b, a (dec a))

Examples
--------

    ~zod/try=> (snag 2 "asdf")
    ~~d
    ~zod/try=> (snag 0 `(list ,@ud)`~[1 2 3 4])
    1

------------------------------------------------------------------------

### `++sort`

Quicksort

Quicksort: accepts a [`++list`]() `a` and a [gate]() `b` which accepts two nouns and
produces a loobean. `++sort` then produces a list of the elements of `a`,
sorted according to `b`.

Accepts
-------

`b` is a gate that ccepts two nouns and produces a boolean.

Produces
--------

A list

Source
------

    ++  sort                                                ::  quicksort
      ~/  %sort
      |*  [a=(list) b=$+([* *] ?)]
      =>  .(a ^.(homo a))
      |-  ^+  a
      ?~  a  ~
      %+  weld
        $(a (skim t.a |=(c=_i.a (b c i.a))))
      ^+  t.a
      [i.a $(a (skim t.a |=(c=_i.a !(b c i.a))))]

Examples
--------

        ~zod/try=> =a =|([p=@ q=@] |.((gth p q)))
        ~zod/try=> (sort (limo [0 1 2 3 ~]) a)
        ~[3 2 1 0]

------------------------------------------------------------------------

### `++swag`

Infix

Similar to `substr` in Javascript: extracts a string infix, beginning at
inclusive index `a`, producing `b` number of characters.

Accepts
-------

`c` is a [list]().

Produces
--------

A list of the same type as `c`.

Source
------

    ++  swag                                                ::  infix
      |*  [[a=@ b=@] c=(list)]
      (scag b (slag a c))

Examples
--------

    ~zod/try=> (swag [2 5] "roly poly")
    "ly po"
    ~zod/try=> (swag [2 2] (limo [1 2 3 4 ~]))
    [i=3 t=[i=4 t=~]]

------------------------------------------------------------------------

### `++turn`

Gate to list

Accepts a [`++list`]() `a` and a [gate]() `b`. Produces a list with the gate applied
to each element of the original list.

Accepts
-------

`b` is a gate.

Produces
--------

A list.

Source
------

    ++  turn                                                ::  transform
      ~/  %turn
      |*  [a=(list) b=_,*]
      |-
      ?~  a  ~
      [i=(b i.a) t=$(a t.a)]

Examples
--------

    ~zod/try=> (turn (limo [104 111 111 110 ~]) ,@t)
    <|h o o n|>
    ~zod/try=> =a |=(a=@ (add a 4))
    ~zod/try=> (turn (limo [1 2 3 4 ~]) a)
    ~[5 6 7 8]

------------------------------------------------------------------------

### `++weld`

Concatenate

Concatenate two [`++list`]()s `a` and `b`.

Accepts
-------

`a` and `b` are [list]()s.

Source
------

    ++  weld                                                ::  concatenate
      ~/  %weld
      |*  [a=(list) b=(list)]
      =>  .(a ^.(homo a), b ^.(homo b))
      |-  ^+  b
      ?~  a  b
      [i.a $(a t.a)]

Examples
--------

    ~zod/try=> (weld "urb" "it")
    ~[~~u ~~r ~~b ~~i ~~t]
    ~zod/try=> (weld (limo [1 2 ~]) (limo [3 4 ~]))
    ~[1 2 3 4]

------------------------------------------------------------------------

### `++welp`

Perfect weld

Concatenate two [`++list`]()s `a` and `b` without losing their type information
to homogenization.

Accepts
-------

`a` is a list.

`b` is a list.

Produces
--------

A list.

Source
------

    ++  welp                                                ::  perfect weld
      =|  [* *]
      |%
      +-  $
        ?~  +<-
          +<-(. +<+)
        +<-(+ $(+<- +<->))
      --

Examples
--------

    ~zod/try=> (welp "foo" "bar")
    "foobar"
    ~zod/arvo=/hoon/hoon> (welp ~[60 61 62] ~[%a %b %c])
    [60 61 62 %a %b %c ~]
    ~zod/arvo=/hoon/hoon> :type; (welp ~[60 61 62] ~[%a %b %c])
    [60 61 62 %a %b %c ~]
    [@ud @ud @ud %a %b %c %~]
    ~zod/arvo=/hoon/hoon> (welp [sa/1 so/2 ~] si/3)
    [[%sa 1] [%so 2] %si 3]

------------------------------------------------------------------------

### `++wild`

XXDELETE

    ++  wild                                                ::  concatenate
      |*  [a=(list) b=(list)]
      =>  .(a ^.(homo a), b ^.(homo b))
      |-
      ?~  a  b
      [i=i.a $(a t.a)]

Concatenates two lists `a` and `b`, homogenizing their types
individually.

`a` and `b` are [list]()s.

    ~zod/try=> (wild (limo ~[1 2 3]) (limo [4]~))
    ~[1 2 3 4]
    ~zod/try=> (wild (limo 60 61 62 ~) (limo %a %b %c ~))
    [i=60 [i=61 [i=62 ~[%a %b %c]]]]
    ~zod/try=> (weld (limo 60 61 62 ~) (limo %a %b %c ~))
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.000 15].[10.016 57]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.001 15].[10.016 57]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.002 15].[10.016 57]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.004 15].[10.016 57]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.006 15].[10.016 57]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.006 29].[10.006 44]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.056 3].[10.061 13]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.058 3].[10.061 13]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.059 3].[10.061 13]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[10.060 5].[10.060 47]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.826 7].[9.844 35]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.827 7].[9.844 35]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.827 11].[9.838 13]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.828 11].[9.838 13]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.829 13].[9.831 47]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.830 13].[9.831 47]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.706 7].[9.712 25]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.707 7].[9.712 25]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.708 7].[9.712 25]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.712 7].[9.712 25]>
    ! /~zod/arvo/~2014.10.2..22.58.23..5af9/hoon/:<[9.712 8].[9.712 25]>
    ! exit

------------------------------------------------------------------------

### `++zing`

Cons

Turns a [`++list`]() of lists into a single list by promoting the elements of
each sublist into the higher.

Accepts
-------

A list of lists.

Produces
--------

A list.

Source
------

    ++  zing                                                ::  promote
      =|  *
      |%
      +-  $
        ?~  +<
          +<
        (welp +<- $(+< +<+))
      --

Examples
--------

    ~zod/try=> (zing (limo [(limo ['a' 'b' 'c' ~]) (limo ['e' 'f' 'g' ~]) (limo ['h' 'i' 'j' ~]) ~]))
    ~['a' 'b' 'c' 'e' 'f' 'g' 'h' 'i' 'j']
    ~zod/try=> (zing (limo [(limo [1 'a' 2 'b' ~]) (limo [3 'c' 4 'd' ~]) ~]))
    ~[1 97 2 98 3 99 4 100]
