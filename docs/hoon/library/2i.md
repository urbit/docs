---
navhome: /docs/
---


# 2i: map logic
### `++by`

Map operations

    ++  by                                                  ::  map engine
      ~/  %by
      |_  a/(map)


Container arm for map operation arms. A map is a set of key, value
pairs. The contained arms inherit it's sample map, `a`.

`a` is a map.

    > ~(. by (my [%a 1] [%b 2] ~))
    <22.znh {a/nlr({p/?($a $b) q/@ud}) <409.ofg 110.xht 1.ztu $151>}>

------------------------------------------------------------------------

### `+-all:by`

Logical AND

      +-  all                                               ::  logical AND
        ~/  %all
        |*  b/$-(* ?)
        |-  ^-  ?
        ?~  a
          &
        ?&((b q.n.a) $(a l.a) $(a r.a))
      ::


Computes the logical AND on the results of slamming every element in map
`a` with gate `b`. Produces a loobean.

`a` is a map.

`b` is a wet gate.

    > =b (my [['a' 1] ['b' [2 3]] ~])
    > (~(all by b) |=(a/* ?@(a & |)))
    %.n
    > =a (my [['a' 1] ['b' 2] ['c' 3] ['d' 4] ['e' 5] ~])
    > (~(all by a) |=(a/@ (lte a 6)))
    %.y
    > (~(all by a) |=(a/@ (lte a 4)))
    %.n

------------------------------------------------------------------------

### `+-any:by`

Logical OR

      +-  any                                               ::  logical OR
        ~/  %any
        |*  b=$+(* ?)
        |-  ^-  ?
        ?~  a
          |
        ?|((b q.n.a) $(a l.a) $(a r.a))

Computes the logical OR on the results of slamming every element with
gate `b`. Produces a loobean.

`a` is a map.

`b` is a wet gate.

    > =b (my [['a' 1] ['b' [2 3]] ~])
    > (~(any by b) |=(a/* ?@(a & |)))
    %.y
    > =a (my [['a' 1] ['b' 2] ['c' 3] ['d' 4] ['e' 5] ~])
    > (~(any by a) |=(a/@ (lte a 4)))
    %.y

------------------------------------------------------------------------

### `+-del:by`

Delete

      +-  del                                               ::  delete at key b
        ~/  %del
        |*  b/*
        |-  ^+  a
        ?~  a
          ~
        ?.  =(b p.n.a)
          ?:  (gor b p.n.a)
            [n.a $(a l.a) r.a]
          [n.a l.a $(a r.a)]
        |-  ^-  {$?($~ _a)}
        ?~  l.a  r.a
        ?~  r.a  l.a
        ?:  (vor p.n.l.a p.n.r.a)
          [n.l.a l.l.a $(l.a r.l.a)]
        [n.r.a $(r.a l.r.a) r.r.a]
      ::

Produces map `a` with the element located at key `b` removed.

`a` is a map.

`b` is a key as a noun.

        > =b (my [['a' 1] ['b' [2 3]] ~])
        > (~(del by b) 'a')
        {[p='b' q=[2 3]]}

------------------------------------------------------------------------

### `+-dig:by`

Axis of key

      +-  dig                                               ::  axis of b key
        |=  b/*
        =+  c=1
        |-  ^-  (unit @)
        ?~  a  ~
        ?:  =(b p.n.a)  [~ u=(peg c 2)]
        ?:  (gor b p.n.a)
          $(a l.a, c (peg c 6))
        $(a r.a, c (peg c 7))
      ::


Produce the axis of key `b` within map `a`.

`a` is a map.

`b` is a key as a noun.

        > =b (my [['a' 1] ['b' [2 3]] ~])  
        > (~(dig by b) 'b')
        [~ 2]

------------------------------------------------------------------------

### `+-gas:by`

Concatenate

      +-  gas                                               ::  concatenate
        ~/  %gas
        |=  b/(list _?>(?=(^ a) n.a))
        |-  ^+  a
        ?~  b
          a
        $(b t.b, a (put i.b))
      ::


Insert a list of key-value pairs `b` into map `a`.

`a` is a map.

`b` is a list of cells of key-value nouns `p` and `q`.

    > =a (my [['a' 1] ['b' 2] ~])
    > =b [['c' 3] ['d' 4] ~]
    > (~(gas by a) b)
    {[p='d' q=4] [p='a' q=1] [p='c' q=3] [p='b' q=2]}

------------------------------------------------------------------------

### `+-get:by`

Grab unit value

      +-  get                                               ::  unit value by key
        ~/  %get
        |*  b=*
        |-  ^-  ?(~ [~ u=_?>(?=(^ a) q.n.a)])
        ?~  a
          ~
        ?:  =(b p.n.a)
          [~ u=q.n.a]
        ?:  (gor b p.n.a)
          $(a l.a)
        $(a r.a)

Produce the unit value of the value located at key `b` within map `a`.

`a` is a map

`b` is a key as a noun

        > =b (my [['a' 1] ['b' [2 3]] ~])  
        > (~(get by b) 'b')
        [~ [2 3]]

------------------------------------------------------------------------

### `+-got:by`

Assert

      +-  got
        |*  b/*
        (need (get b))

Produce the value located at key `b` within map `a`. Crash if key `b`
does not exist.

`a` is a map.

`b` is a key.

        > =m (my [['a' 1] ['b' 2] ~])
        > m
        {[p='a' q=1] [p='b' q=2]}
        > (~(get by m) 'a')
        [~ 1]
        > (~(got by m) 'a')
        1
        > (~(got by m) 'c')
        ! exit

------------------------------------------------------------------------

### `+-has:by`

Key existence check

      +-  has                                               ::  key existence check
        ~/  %has
        |*  b/*
        !=(~ (get b))


Checks whether map `a` contains an element with key `b`, producing a
loobean.

`a` is a map.

`b` is a key as a noun.

        > =b (my [['a' 1] ['b' [2 3]] ~])  
        > (~(has by b) 'b')
        %.y
        > (~(has by b) 'c')
        %.n

------------------------------------------------------------------------

### `+-int:by`

Intersection

      +-  int                                               ::  intersection
        ~/  %int
        |*  b/_a
        |-  ^+  a
        ?~  b
          ~
        ?~  a
          ~
        ?:  (vor p.n.a p.n.b)
          ?:  =(p.n.b p.n.a)
            [n.b $(a l.a, b l.b) $(a r.a, b r.b)]
          ?:  (gor p.n.b p.n.a)
            %-  uni(a $(a l.a, b [n.b l.b ~]))  $(b r.b)
          %-  uni(a $(a r.a, b [n.b ~ r.b]))  $(b l.b)
        ?:  =(p.n.a p.n.b)
          [n.b $(b l.b, a l.a) $(b r.b, a r.a)]
        ?:  (gor p.n.a p.n.b)
          %-  uni(a $(b l.b, a [n.a l.a ~]))  $(a r.a)
        %-  uni(a $(b r.b, a [n.a ~ r.a]))  $(a l.a)
      ::

Produces a map of the (key) intersection between two maps of the same
type, `a` and `b`. If both maps have an identical key that point to
different values, the element from map `b` is used.

`a` is a map.

`b` is a map.

        > =n (my [['a' 1] ['c' 3] ~])
        > n
        {[p='a' q=1] [p='c' q=3]}
        > m
        {[p='a' q=1] [p='b' q=2]}
        > (~(int by m) n)
        {[p='a' q=1]}
        > =p (my [['a' 2] ['b' 2] ~])
        > p
        {[p='a' q=2] [p='b' q=2]}
        > (~(int by p) n)
        {[p='a' q=1]}
        > =q (my [['a' 2] ['c' 2] ~])
        > q
        {[p='a' q=2] [p='c' q=2]}
        > (~(int by p) q)
        {[p='a' q=2]}
        > =o (my [['c' 3] ['d' 4] ~])
        > (~(int by m) o)
        {}


------------------------------------------------------------------------

### `+-mar:by`

Assert and Add

      +-  mar                                               ::  add with validation
        |*  [b=_?>(?=(^ a) p.n.a) c=(unit ,_?>(?=(^ a) q.n.a))]
        ?~  c
          (del b)
        (put b u.c)

Produces map `a` with the addition of a key-value pair, where the value
is a nonempty unit.

Accept a noun and a unit of a noun of the type of the map's keys and
values, respectively. Validate that the value is not null and put the
pair in the map. If the value is null, delete the key.

XX This arm is broken, asana task 15186618346453

        > m
        {[p='a' q=1] [p='b' q=2]}
        > (~(mar by m) 'c' (some 3))
        ! -find-limb.n
        ! find-none
        ! exit
        > (~(mar by m) 'c' ~)
        ! -find-limb.n
        ! find-none
        ! exit
        > (~(mar by m) 'b' ~)
        ! -find-limb.n
        ! find-none
        ! exit

------------------------------------------------------------------------

### `+-put:by`

Add key-value pair

      +-  put                                               ::  adds key-value pair
        ~/  %put
        |*  {b/* c/*}
        |-  ^+  a
        ?~  a
          [[b c] ~ ~]
        ?:  =(b p.n.a)
          ?:  =(c q.n.a)
            a
          [[b c] l.a r.a]
        ?:  (gor b p.n.a)
          =+  d=$(a l.a)
          ?>  ?=(^ d)
          ?:  (vor p.n.a p.n.d)
            [n.a d r.a]
          [n.d l.d [n.a r.d r.a]]
        =+  d=$(a r.a)
        ?>  ?=(^ d)
        ?:  (vor p.n.a p.n.d)
          [n.a l.a d]
        [n.d [n.a l.a l.d] r.d]
      ::

Produces `a` with the addition of the key-value pair of `b` and `c`.

`a` is a map.

`b` is a key of the same type as the keys in `a`.

`c` is a value of the same type of the values in `a`.

    > m
    {[p='a' q=1] [p='b' q=2]}
    > (~(put by m) 'c' 3)
    {[p='a' q=1] [p='c' q=3] [p='b' q=2]}
    > (~(put by m) "zod" 26)
    ! type-fail
    ! exit
    > (~(put by m) 'a' 2)
    {[p='a' q=2] [p='b' q=2]}

------------------------------------------------------------------------

### `+-rep:by`

      +-  rep                                               ::  replace by product
        |*  b/_|=({* *} +<+)
        |-
        ?~  a  +<+.b
        $(a r.a, +<+.b $(a l.a, +<+.b (b n.a +<+.b)))
      ::

Accumulate using gate from values in map

XX interface changing.

------------------------------------------------------------------------

### `+-rib:by`

      +-  rib                                               ::  transform + product
        |*  {b/* c/$-(* *)}
        |-  ^+  [b a]
        ?~  a  [b ~]
        =+  d=(c n.a b)
        =.  n.a  +.d
        =+  e=$(a l.a, b -.d)
        =+  f=$(a r.a, b -.e)
        [-.f [n.a +.e +.f]]
      ::

Replace values with accumulator

XX interface changing, possibly disappearing

------------------------------------------------------------------------

### `+-run:by`

Transform values

      +-  run                                               ::  apply gate to values
        |*  b/$-(* *)
        |-
        ?~  a  a
        [n=[p=p.n.a q=(b q.n.a)] l=$(a l.a) r=$(a r.a)]

Iterates over every value in set `a` using gate `b`. Produces a map.

`a` is a map.

`b` is a wet gate.

    > m
    {[p='a' q=1] [p='b' q=2]}
    > ^+(m (~(run by m) dec))
    {[p='a' q=0] [p='b' q=1]}
    > `(map @tas @t)`(~(run by m) (cury scot %ux))
    {[p=%a q='0x1'] [p=%b q='0x2']}

------------------------------------------------------------------------

### `+-tap:by`

Listify pairs

      +-  tap                                               ::  listify pairs
        ~/  %tap
        |=  b/(list _?>(?=(^ a) n.a))
        ^+  b
        ?~  a
          b
        $(a r.a, b [n.a $(a l.a)])
      ::

Produces the list of all elements in map `a` that is prepended to list
`b`, which is empty by default.

`a` is a map.

`b` is a list.

    > m
    {[p='a' q=1] [p='b' q=2]}
    > `*`m
    [[98 2] [[97 1] 0 0] 0]
    > (~(tap by m))
    ~[[p='b' q=2] [p='a' q=1]]
    > `*`(~(tap by m))
    [[98 2] [97 1] 0]

------------------------------------------------------------------------

### `+-uni:by`

Union

      +-  uni                                               ::  union, merge
        ~/  %uni
        |*  b/_a
        |-  ^+  a
        ?~  b
          a
        ?~  a
          b
        ?:  (vor p.n.a p.n.b)
          ?:  =(p.n.b p.n.a)
            [n.b $(a l.a, b l.b) $(a r.a, b r.b)]
          ?:  (gor p.n.b p.n.a)
            $(a [n.a $(a l.a, b [n.b l.b ~]) r.a], b r.b)
          $(a [n.a l.a $(a r.a, b [n.b ~ r.b])], b l.b)
        ?:  =(p.n.a p.n.b)
          [n.b $(b l.b, a l.a) $(b r.b, a r.a)]
        ?:  (gor p.n.a p.n.b)
          $(b [n.b $(b l.b, a [n.a l.a ~]) r.b], a r.a)
        $(b [n.b l.b $(b r.b, a [n.a ~ r.a])], a l.a)

Produces a map of the union between the keys of `a` and `b`. If `b`
shares a key with `a`, the tuple from `b` is preserved.

`a` is a map.

`b` is a map.

    > m
    {[p='a' q=1] [p='b' q=2]}
    > o
    {[p='d' q=4] [p='c' q=3]}
    > (~(uni by m) o)
    {[p='d' q=4] [p='a' q=1] [p='c' q=3] [p='b' q=2]}
    > (~(uni by m) ~)
    {[p='a' q=1] [p='b' q=2]}
    > n
    {[p='a' q=1] [p='c' q=3]}
    > (~(uni by o) n)
    {[p='d' q=4] [p='a' q=1] [p='c' q=3]}

------------------------------------------------------------------------

### `+-urn:by`

Turn (with key)

      +-  urn                                               ::  apply gate to nodes
        |*  b/$-({* *} *)
        |-
        ?~  a  ~
        [n=[p=p.n.a q=(b p.n.a q.n.a)] l=$(a l.a) r=$(a r.a)]

Iterates over every value in map `a` using gate `b`, which accepts both
the key and the value of each element as its sample.

`a` is a map.

`b` is a wet gate that accepts two nouns (a key and a value) and
produces a noun (the new value).

    > m
    {[p='a' q=1] [p='b' q=2]}

    > (~(urn by m) |=(a/(pair * *) q.a))
    {[p='a' q=1] [p='b' q=2]}

    > (~(urn by m) |=(a/(pair * *) 7))
    {[p='a' q=7] [p='b' q=7]}

    > (~(urn by m) |=(a/(pair * *) p.a))
    {[p='a' q=97] [p='b' q=98]}

------------------------------------------------------------------------

### `+-wyt:by`

Depth

      +-  wyt                                               ::  depth of map
        |-  ^-  @
        ?~(a 0 +((add $(a l.a) $(a r.a))))

Produce the depth of the tree map `a`.

`a` is a map.

    > m
    {[p='a' q=1] [p='b' q=2]}
    > o
    {[p='d' q=4] [p='c' q=3]}
    > ~(wyt by m)
    2
    > ~(wyt by o)
    2
    > ~(wyt by (~(uni by m) o))
    4



***
